sort Wafer = struct wafer( Id : Int, treated:Bool );

map Dummy : Wafer;
eqn Dummy = wafer( -1, false );

map IsTreated : Wafer -> Bool;
var id:Int;
	t:Bool;
eqn IsTreated(wafer(id,t)) = t;

sort WaferSet = Set( Wafer );
sort WaferList = List( Wafer );
map  InitWaferList: Nat -> WaferList;
var  n : Nat;
eqn  InitWaferList(0)     = [];
0 < n -> InitWaferList(n) = wafer( n, false ) |> InitWaferList( max( n - 1, 0 ) );

%Define robot constants
sort RobotID = struct Ra | Rb | Rc;
sort SluiceID = struct S0 | S1 | S2;

act
	%Vital steps
	TreatWafer : Wafer; %Treat a wafer
	TakeWafer : RobotID # Wafer; %Robot takes wafer
	DepositWafer : Wafer; %Robot reposits wafer

	%Communication steps
	S2R : RobotID # SluiceID # Wafer;
	R2S : RobotID # SluiceID # Wafer;
	P2R : Wafer;
	R2P : Wafer;

	%Comm parts
	S2R_R, S2R_S : RobotID # SluiceID # Wafer; %Sluices to robot
	R2S_R, R2S_S : RobotID # SluiceID # Wafer; %Robot to sluices
	R2P_R, R2P_P : Wafer; %Robot to projector
	P2R_R, P2R_P: Wafer; %Projector to robot

	CloseInnerDoor: SluiceID;
	CloseOuterDoor: SluiceID;
	OpenInnerDoor: SluiceID;
	OpenOuterDoor: SluiceID;
	PumpVacuum: SluiceID;
	ReleaseVacuum: SluiceID;

map TreatedWafer : Wafer -> Wafer;
var n:Int;
	b:Bool;
eqn
	TreatedWafer(wafer(n, b)) = wafer( n, true ); 

sort SluiceDoorState = struct inner_open | closed | outer_open; 

proc
	%RobotHandler
	R( input:WaferList, rack:WaferSet, output:WaferSet ) = 
		( ( input != [] ) -> TakeWafer( Ra, head( input ) ) . sum s:SluiceID . R2S_R( Ra, s, head( input ) ) . R( tail( input ), rack, output ) ) +
		( sum w:Wafer, s:SluiceID . (IsTreated(w)) -> S2R_R( Ra, s, w ) . DepositWafer( w ) . R( input, rack, output + {w} ) ) +
		( sum w:Wafer, s:SluiceID . (!IsTreated(w)) -> S2R_R( Rb, s, w ) . R( input, rack + {w}, output ) ) +
		( sum w:Wafer . (w in rack && !IsTreated(w) ) -> R2P_R( w ) . R( input, rack - {w}, output ) ) +
		( sum w:Wafer . (IsTreated(w)) -> P2R_R( w ) . R( input, rack + {w}, output ) ) +
		( sum w:Wafer, s:SluiceID . (w in rack && IsTreated(w) ) -> R2S_R( Rb, s, w ) . R( input, rack - {w}, output ) );

	%SluiceHandler
	S( id:SluiceID, occupied:Bool, w:Wafer, door:SluiceDoorState ) =
		((!occupied) -> (
			(door == outer_open ) -> sum w2:Wafer . R2S_S( Ra, id, w2 ) . CloseOuterDoor( id ) . PumpVacuum( id ) . S( id, true, w2, closed ) +
			(door == inner_open ) -> sum w2:Wafer . R2S_S( Rb, id, w2 ) . CloseInnerDoor( id ) . ReleaseVacuum( id ) . S( id, true, w2, closed )
		) <> (
			(door == outer_open ) -> S2R_S( Ra, id, w ) . S( id, false, Dummy, door ) + 
			(door == inner_open ) -> S2R_S( Rb, id, w ) . S( id, false, Dummy, door )
		)) +
		(door == closed && IsTreated( w )) -> OpenOuterDoor( id ) . S( id, occupied, w, outer_open ) +
		(door == closed && !IsTreated( w )) -> OpenInnerDoor( id ) . S( id, occupied, w, inner_open )
	;

	%Projector
	P( occupied : Bool, w : Wafer ) =
		occupied ->
			TreatWafer( w ) . P2R_P( TreatedWafer( w ) ).P( false, Dummy )
		<>
			sum w:Wafer . R2P_P( w ) . P( true, w );

init
	%hide({ R2S, S2R, R2P, P2R },
		allow({ R2S, S2R, R2P, P2R, TreatWafer, TakeWafer, DepositWafer, CloseOuterDoor, CloseInnerDoor, OpenInnerDoor, OpenOuterDoor, PumpVacuum, ReleaseVacuum },
			comm({ R2S_R|R2S_S -> R2S, S2R_S|S2R_R -> S2R, R2P_R|R2P_P -> R2P, P2R_P|P2R_R -> P2R },
				R( InitWaferList( 2 ), {}, {} ) ||
				S( S0, false, Dummy, outer_open ) ||
				S( S1, false, Dummy, outer_open ) ||
				P( false, Dummy )
			)
		%)
	);