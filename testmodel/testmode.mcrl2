sort Wafer = struct wafer( Id : Int, treated:Bool );

map Dummy : Wafer;
eqn Dummy = wafer( -1, false );

%Wafer in projector
map WIP : Bool;
eqn WIP = false;

map IsTreated : Wafer -> Bool;
var id:Int;
	t:Bool;
eqn IsTreated(wafer(id,t)) = t;

sort WaferSet = Set( Wafer );
sort WaferList = List( Wafer );
map  InitWaferList: Nat -> WaferList;
var  n : Nat;
eqn  InitWaferList(0)     = [];
0 < n -> InitWaferList(n) = wafer( n, false ) |> InitWaferList( max( n - 1, 0 ) );

%Define robot constants
map Ra, Rb, Rc : Int;
eqn Ra = 0;
	Rb = 1;
	Rc = 2;

act
	%Vital steps
	TreatWafer : Wafer; %Treat a wafer
	TakeWafer : Int # Wafer; %Robot takes wafer
	DepositWafer : Wafer; %Robot reposits wafer

	%Communication steps
	S2R : Int # Wafer;
	R2S : Int # Wafer;
	P2R : Wafer;
	R2P : Wafer;

	%Comm parts
	S2R_R, S2R_S : Int # Wafer; %Sluices to robot
	R2S_R, R2S_S : Int # Wafer; %Robot to sluices
	R2P_R, R2P_P : Wafer; %Robot to projector
	P2R_R, P2R_P: Wafer; %Projector to robot

map TreatedWafer : Wafer -> Wafer;
var n:Int;
	b:Bool;
eqn
	TreatedWafer(wafer(n, b)) = wafer( n, true ); 

proc
	%RobotHandler
	R( input:WaferList, rack:WaferSet, output:WaferSet ) = 
		( ( input != [] ) -> TakeWafer( Ra, head( input ) ) . R2S_R( Ra, head( input ) ) . R( tail( input ), rack, output ) ) +
		( sum w:Wafer . (IsTreated(w)) -> S2R_R( Ra, w ) . DepositWafer( w ) . R( input, rack, output + {w} ) ) +
		( sum w:Wafer . (!IsTreated(w)) -> S2R_R( Rb, w ) . R( input, rack + {w}, output ) ) +
		( sum w:Wafer . (w in rack && !IsTreated(w) ) -> R2P_R( w ) . R( input, rack - {w}, output ) ) +
		( sum w:Wafer . (IsTreated(w)) -> P2R_R( w ) . R( input, rack + {w}, output ) ) +
		( sum w:Wafer . (w in rack && IsTreated(w) ) -> R2S_R( Rb, w ) . R( input, rack - {w}, output ) );

	%SluiceHandler
	S( wl:WaferList ) =
		( sum w:Wafer . R2S_S( Ra, w ) . S( wl <| w ) ) +
		( sum w:Wafer . R2S_S( Rb, w ) . S( wl <| w ) ) +
		( (wl != [] ) -> S2R_S( Ra, head( wl ) ) . S( tail( wl ) ) ) + 
		( (wl != [] ) -> S2R_S( Rb, head( wl ) ) . S( tail( wl ) ) );

	%Projector
	P( occupied : Bool, w : Wafer ) =
		occupied ->
			TreatWafer( w ) . P2R_P( TreatedWafer( w ) ).P( false, Dummy )
		<>
			sum w:Wafer . R2P_P( w ) . P( true, w );

init
	%hide({ R2S, S2R, R2P, P2R },
		allow({ R2S, S2R, R2P, P2R, TreatWafer, TakeWafer, DepositWafer },
			comm({ R2S_R|R2S_S -> R2S, S2R_S|S2R_R -> S2R, R2P_R|R2P_P -> R2P, P2R_P|P2R_R -> P2R },
				R( InitWaferList( 2 ), {}, {} ) ||
				S( [] ) ||
				P( false, Dummy )
			)
		%)
	);